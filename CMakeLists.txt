cmake_minimum_required(VERSION 3.26)

# Project.
project(framebuffet)
set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

# External dependencies.
add_subdirectory(external)

# Framebuffet.
add_executable(${PROJECT_NAME} src/main.cpp src/app.manifest)
target_compile_options(${PROJECT_NAME} PRIVATE /W4 /WX /MP)
set_property(DIRECTORY ${PROJECT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT ${PROJECT_NAME})
set_target_properties(${PROJECT_NAME} PROPERTIES VS_DEBUGGER_WORKING_DIRECTORY "$<TARGET_FILE_DIR:${PROJECT_NAME}>")
target_include_directories(
    ${PROJECT_NAME} PRIVATE
    ${WIL_INCLUDE_DIR}
    ${D3D12MEMALLOC_INCLUDE_DIR}
    ${DXAGILITYSDK_INCLUDE_DIR}
    ${DXCOMPILER_INCLUDE_DIR}
    ${DIRECTXTK12_INCLUDE_DIR}
    ${DIRECTXMESH_INCLUDE_DIR}
    ${DIRECTXTEX_INCLUDE_DIR}
)
target_link_libraries(
    ${PROJECT_NAME} PRIVATE
    d3d12.lib
    dxgi.lib
    dxguid.lib
    D3D12MemoryAllocator
    dxagilitysdk
    ${DXCOMPILER_LINK_DIR}/dxcompiler.lib
    ${DIRECTXTK12_LINK_DIR}/DirectXTK12.lib
    ${DIRECTXMESH_LINK_DIR}/DirectXMesh.lib
    ${DIRECTXTEX_LINK_DIR}/DirectXTex.lib
)

# Copy assets to target directory.
set(PROJECT_TARGET_DIRECTORY $<TARGET_FILE_DIR:${PROJECT_NAME}>)
add_custom_command(
    TARGET ${PROJECT_NAME} POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E make_directory ${PROJECT_TARGET_DIRECTORY}/shaders
    COMMAND ${CMAKE_COMMAND} -E copy ${CMAKE_SOURCE_DIR}/src/triangle.hlsl ${PROJECT_TARGET_DIRECTORY}/shaders/
    COMMAND ${CMAKE_COMMAND} -E copy ${DXAGILITYSDK_SOURCE_DIR}/bin/x64/D3D12Core.dll ${PROJECT_TARGET_DIRECTORY}
    COMMAND ${CMAKE_COMMAND} -E copy ${DXAGILITYSDK_SOURCE_DIR}/bin/x64/D3D12Core.pdb ${PROJECT_TARGET_DIRECTORY}
    COMMAND ${CMAKE_COMMAND} -E copy ${DXAGILITYSDK_SOURCE_DIR}/bin/x64/d3d12SDKLayers.dll ${PROJECT_TARGET_DIRECTORY}
    COMMAND ${CMAKE_COMMAND} -E copy ${DXAGILITYSDK_SOURCE_DIR}/bin/x64/d3d12SDKLayers.pdb ${PROJECT_TARGET_DIRECTORY}
    COMMAND ${CMAKE_COMMAND} -E copy ${DXCOMPILER_SOURCE_DIR}/bin/x64/dxcompiler.dll ${PROJECT_TARGET_DIRECTORY}
    COMMAND ${CMAKE_COMMAND} -E copy ${DXCOMPILER_SOURCE_DIR}/bin/x64/dxcompiler.pdb ${PROJECT_TARGET_DIRECTORY}
    COMMAND ${CMAKE_COMMAND} -E copy ${DXCOMPILER_SOURCE_DIR}/bin/x64/dxil.dll ${PROJECT_TARGET_DIRECTORY}
    COMMAND ${CMAKE_COMMAND} -E copy ${DXCOMPILER_SOURCE_DIR}/bin/x64/dxil.pdb ${PROJECT_TARGET_DIRECTORY}
)
